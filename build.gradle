subprojects {

  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'maven-publish'

  group = 'com.unideb.qsa'
  version = project.getProperties().get('release.version') ?: 'RELEASE.VERSION.UNKNOWN'
  sourceCompatibility = '11'
  targetCompatibility = '11'

  checkstyle {
    toolVersion = libs.versions.checkstyle.get()
    showViolations = true
    ignoreFailures = false
  }

  tasks.withType((Class) JavaCompile) {
    options.encoding = 'UTF-8'
  }

  clean {
    delete "${projectDir}/src/main/.aws-sam/"
  }

  test {
    useTestNG()
    scanForTestClasses = false
    testLogging {
      afterSuite { desc, result ->
        if (!desc.parent) {
          println "Results: ${result.getResultType()} (${result.getTestCount()} tests, ${result.getSuccessfulTestCount()} successes, ${result.getFailedTestCount()} failures, ${result.getSkippedTestCount()} skipped)"
        }
      }
    }
  }

  if (it.name == 'qsa-i18n-retriever' || it.name == 'qsa-i18n-updater') {
    task buildZip(type: Zip) {
      from compileJava
      from processResources
      into('lib') {
        from configurations.runtimeClasspath
      }
    }
  }
  publishing {
    repositories {
      maven {
        name = 'GitHubPackages'
        url = uri('https://maven.pkg.github.com/queueing-systems-assistance/qsa-i18n-service')
        credentials {
          username = System.getenv('GITHUB_USERNAME') ?: 'YOUR_USERNAME'
          password = System.getenv('GITHUB_TOKEN') ?: 'YOUR_TOKEN'
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }
}
